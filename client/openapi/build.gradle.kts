plugins {
    java
    id("org.openapi.generator") version "7.5.0"
}

// It's an open question which version of Java we should target for this client library;
// 21 is not necessary, as the generated sources do not use newer language features.
// Java 11 has been tested and works fine, but the value below has been left at 21 for now
// to ensure that building this larger project requires only having JDK 21 installed.
//
// NOTE: this bisq:openapi-java-client library is not intended for use on Android clients.
// A dedicated bisq:openapi-android-client library will be generated for that purpose,
// with its own build that will target a Java version supported by Android. [1]
// See https://developer.android.com/build/jdks.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
    mavenCentral()
}

dependencies {
    // Each of the following are required (compile-time) dependencies of the
    // classes generated by the openApiGenerate task
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.12.0")
    implementation("io.gsonfire:gson-fire:1.9.0")
    implementation("org.openapitools:jackson-databind-nullable:0.2.6")
    implementation("jakarta.annotation:jakarta.annotation-api:1.3.5")
}

// The base package for classes generated by the openApiGenerate task
var basePackage = "bisq.client.openapi"

// Configure the OpenAPI Tools Generator per the docs at [1]
openApiGenerate {
    groupId = "bisq"
    id = "openapi-java-client"
    generatorName = "java"
    inputSpec = "$rootDir/core/openapi/build/classes/java/main/META-INF/swagger/bisq-openapi-2.1.0.yml"
    outputDir = projectDir.path
    invokerPackage = basePackage
    apiPackage = "${basePackage}.endpoint"
    modelPackage = "${basePackage}.model"
    apiNameSuffix = "Endpoint"
    generateApiTests = false
    generateModelTests = false
    configOptions.put("hideGenerationTimestamp", "true") // [2]
}

tasks.getByName("openApiGenerate") {
    // openApiGenerate will fail if its `inputSpec` (above) exists, and this file is
    // created by annotation processing that runs during the :core:openapi:compileJava
    // task. The task dependency below ensures that spec generation happens before this
    // task runs.
    //
    // NOTE: openApiGenerate will only run if its `inputSpec` has actually changed since
    // the last run; otherwise it will consider itself UP-TO-DATE as per Gradle's
    // incremental build support. [3]
    dependsOn(":core:openapi:compileJava")

    // The following `doFirst` logic ensures that any previously generated sources are
    // removed before the openApiGenerate task runs and re-generates them. Doing this is
    // critical to ensure that model and controller classes that get renamed or removed in
    // bisq.core.*.api packages have their corresponding endpoint and model classes deleted
    // appropriately here in the generated bisq.client.openapi.* packages. Failing to do
    // this deletion can easily lead to dead and outdated copies of endpoint and model
    // classes sticking around in the codebase. Remember that the sources being generating
    // here are not ephemeral: they get generated into the src/ directory as opposed to the
    // build/ directory and get checked into source control. This means they will not just
    // "go away" with the next invocation of `gradle clean`. We must instead explicitly
    // remove them and that's what happens below.
    doFirst {
        delete(
            "api",          // generated openapi.yaml spec
            "src",          // generated client sources
            "docs",         // generated api usage documentation
            "README.md"     // generated api usage documentation
        )
    }
}

// This task dependency ensures that the openApiGenerate task gets run as part of the
// normal build lifecycle, e.g. when running `gradle build` at the command line.
tasks.getByName("compileJava").dependsOn("openApiGenerate")

// [1]: https://github.com/OpenAPITools/openapi-generator/blob/master/modules/openapi-generator-gradle-plugin/README.adoc#openapigenerate
// [2]: https://github.com/OpenAPITools/openapi-generator/blob/master/docs/generators/java.md#config-options
// [3]: https://docs.gradle.org/current/userguide/gradle_optimizations.html
